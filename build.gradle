plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id "org.sonarqube" version "2.5"
}

group 'com.github.mictaege'
version '2021.3'

sourceCompatibility = 1.8

wrapper {
    gradleVersion = "6.7"
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
}

sonarqube {
    properties {
        property "sonar.projectName", "com.github.mictaege.arete"
        property "sonar.projectKey", "com.github.mictaege.arete"
    }
}

dependencies {
    implementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.apache.commons:commons-text:1.9'

    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:3.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") } //sign only when upload archives
    sign configurations.archives
}

//don't refer to username and pwd directly so it could be executed in another environment with another _gradle.properties_
String publishUsername = hasProperty("ossrhUsername") ? property("ossrhUsername") : ""
String publishPassword = hasProperty("ossrhPassword") ? property("ossrhPassword") : ""

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            pom.project {
                name 'arete'
                packaging 'jar'
                description 'This repository contains the _Arete_ JUnit5 extention. For using _Arete_ see the https://github.com/mictaege/arete documentation.'
                url 'https://github.com/mictaege/arete'
                scm {
                    connection 'https://github.com/mictaege/arete.git'
                    url 'https://github.com/mictaege/arete'
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'mictaege'
                        name 'Michael Taege'
                    }
                }
            }
        }
    }
}